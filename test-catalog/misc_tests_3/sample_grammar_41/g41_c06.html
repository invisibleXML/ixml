<!DOCTYPE HTML><html xmlns="http://www.w3.org/1999/xhtml"><head><meta http-equiv="Content-Type" content="text/html; charset=utf-8"><title>Invisible XML Test Suite Catalog</title><link rel="stylesheet" href="../../css/catalog.css"></head><body><div class="breadcrumbs"><a href="../../"><img class="logotype" src="../..//img/logotype_white.png"></a>&nbsp;/&nbsp;<a href="../../misc_tests_3">Misc tests 3</a>&nbsp;/&nbsp;<a href="../../misc_tests_3/sample_grammar_41">sample.grammar.41</a>&nbsp;/&nbsp;g41.c06</div><main><img class="logosm" src="../../img/logo_gradient.png"><h1 class="title">iXML Community Group Test Suite</h1><details><summary>21&nbsp;Jun&nbsp;2022 (20&nbsp;Aug&nbsp;2022)</summary><div class="description">
    <p>Top-level catalog for tests in the iXML Community Group
    Test Suite.</p>
    <p>Tests have been contributed from several sources, but
    the core of the test collection are the tests contributed
    by Steven Pemberton in December 2021.</p>
  </div></details><h2 class="title">Misc tests 3</h2><details><summary>21&nbsp;Jun&nbsp;2022</summary><div class="description">
    <p>Grammars 41-60.</p>
    <p>Tests compiled manually in 2018 and 2019, re-packaged and
    extended (supplying test cases where needed) in 2022.</p>
  </div><div class="description">
    <p>Note that some tests have alternate results for processors
    operating in non-standard modes, in particular modes in which
    they tolerate multiple definitions and undefined nonterminals
    or in which they do not tolerate non-productive nonterminals
    or unreachable nonterminals.</p>
    <p>For a description of the form in which alternate results
    are recorded, see tests/misc-grammar/test-catalog.xml.</p>
  </div></details><h3 class="title">sample.grammar.41</h3><p>Created 08&nbsp;Feb&nbsp;2022 by cmsmcq</p><div class="description">
      <p>
	Sample grammar from Niklaus Wirth, Grundlagen und Techniken des
	Compilerbaus (Bonn: Addison-Wesley, 1996), pp. 36-37.
      </p>
      <p>
	Grammar for Oberon-0, the subset of Oberon for which the book
	describes a compiler.
      </p>
      <p>	
	Rules for whitespace and comments added for usability; Wirth
	assumes a lexer to eat whitespace.  This grammar experiments
	with a pattern in which nonterminals normally begin and end
	with non-whitespace characters, so in the XML any whitespace
	goes outside the tags, not inside.  (A different style is
	shown in sample.grammar.41bis.)
      </p>
      <p>
	(For what it's worth: in some ways the raw parse tree produced
	by this grammar is nicer than the raw parse tree produced by
	grammar 41bis, but optional nonterminals and nonterminals
	which can produce the empty string cause ambiguity unless they
	are handled specially.)
      </p>
    </div><div class="grammar"><h4>Invisible XML Grammar</h4><pre><code>{ Sample grammar from Niklaus Wirth, Grundlagen und Techniken des
Compilerbaus (Bonn: Addison-Wesley, 1996), pp. 36-37.

Oberon-0.

Rules for ws added for convenience; Wirth assumes the lexer
eats whitespace. }

{ Revisions:
  2022-06-15 : CMSMcQ : tweaks to align output with grammar 41bis
  2022-06-14 : CMSMcQ : revise S handling (tight tags, where possible).
                        Hide most literals, use pre-terminal symbols.
  2018-08-10 : CMSMcQ : correct syntax errors found by DCG parser
  2018-08-09 : CMSMcQ : made first transcription; needs testing, since
      the translation from Wirth's EBNF is error-prone.
}

{ We move the rule for module to the front, since it is the
start symbol. }

            module = S?, -"MODULE", S, @ident, SEMI,
                     declarations,
                     (-"BEGIN", StatementSequence)?,
                     -"END", S, @ident_close, S?, -".", S?.
    
             ident = letter, (letter; digit)*.
       ident_close = ident.
           integer = digit, (digit)*.
          selector = (member; subscript)++(S?).
	    member = -".", S?, ident.
	 subscript = -"[", S?, expression, S?, -"]".
         {selector = (".", S?, ident; "[", S?, expression, S?, "]")++S.}
            number = integer.
            factor = ident, (S?, selector)?
	           ; number
		   ; -"(", S?, expression, S?, -")"
		   ; "~", S?, factor
		   .
              term = factor++(S?, (TIMES; DIV; MOD; AND), S?).
	      
  SimpleExpression =  (PLUS; MINUS)?, term++(S?, (PLUS; MINUS; OR), S?).

    { N.B. Simplexpression allows 1, -1, 1 + 1,
      -1 + 1, but not 1 + -1.
      I expect that's intentional.
      I've made it also forbid white space between sign and term.
    }    
 
        expression = SimpleExpression, (S?, -COMPARATOR, S?, SimpleExpression)?.
       -COMPARATOR = EQL; NEQ; LSS; LEQ; GTR; GEQ.
        assignment = ident, S?, (selector, S?)?, -":=", S?, expression.
  ActualParameters = -"(", S?, expression**(S?, -",", S?), S?, -")".
     ProcedureCall = ident, (S?, selector)?, (S?, ActualParameters)?.
       IfStatement = -"IF", S?, condition, S?, 
                     -"THEN", then-stmts,
                     (-"ELSIF", S?, condition, S?, 
		     -"THEN", then-stmts)*,
                     (-"ELSE", else-stmts)?,
		     -"END".
         condition = -expression.
        then-stmts = -StatementSequence.
        else-stmts = -StatementSequence.
    WhileStatement = -"WHILE", S?, condition, S?,
                     -"DO", do-statements,
		     -"END".
     do-statements = -StatementSequence.

         statement = S?, ((assignment
			 ; ProcedureCall
			 ; IfStatement
			 ; WhileStatement), S?)?.
 StatementSequence = statement++ -";".
 
 { Wirth's formulation is closer to the following, but that complicates whitespace
   handling. 

         statement = (assignment; ProcedureCall; IfStatement; WhileStatement)?.
 StatementSequence = statement++SEMI.
 }

         IdentList = ident++(-",", S?).
         ArrayType = -"ARRAY", S?, expression, S?, -"OF", S?, type.
         FieldList = (IdentList, COLON, type)?.
        RecordType = -"RECORD", S?, FieldList++SEMI, S?, -"END", S?.
              type = ident
	           ; ArrayType
		   ; RecordType
		   .
         FPSection = (-"VAR", S)?, IdentList, COLON, type.
  FormalParameters = -"(", S?, FPSection**SEMI, S?, -")".
  ProcedureHeading = -"PROCEDURE", S, ident, S?, FormalParameters?.
     ProcedureBody = declarations, 
                     (-"BEGIN", StatementSequence)?,
		     -"END", S, @ident.
ProcedureDeclaration
                   = ProcedureHeading, SEMI, ProcedureBody.
      declarations = (constants, S?)?, (types, S?)?, (variables, S?)?, (procedures, S?)?.
         constants = -"CONST", (S, (ident, EQUALS, expression, S?, -";")++S)?.
             types = -"TYPE", (S, (IdentList, EQUALS, type, S?, -";")++S)?.
         variables = -"VAR", (S, (IdentList, COLON, type, S?, -";")++S)?.
        procedures = (ProcedureDeclaration, S?, -";")++S.

{ S and comment added here to make the grammar usable without
  a scanner. N.B. Comments nest. }
                -S = (ws; comment)+.
               -ws = -[" "; #09; #0A; #0D].

           comment = -"(*", comment-body, -"*)".
      comment-body = comment-chars, ((comment++comment-chars), comment-chars?)?.
    -comment-chars = (cc1; cc2; cc3)+, star*, lpar* | star+, lpar* | lpar+.
              -cc1 = not-star-or-lpar.
              -cc2 = lpar+, not-star-or-lpar.
	      -cc3 = star+, lpar+, not-star-or-lpar
                   | star+, not-star-or-lrpar.
 -not-star-or-lpar = ~["*("].
-not-star-or-lrpar = ~["(*)"].
             -lpar = "(".
	     -star = "*".

            -digit = ['0'-'9'].
           -letter = ['a'-'z'; 'A'-'Z'].

             TIMES = -"*".
	       DIV = -"DIV".
	       MOD = -"MOD".
	       AND = -"AND".
	      PLUS = -"+".
	     MINUS = -"-".
	        OR = -"OR".
               EQL = -"=".
	       NEQ = -"#".
	       LSS = -"&lt;".
	       LEQ = -"&lt;=".
	       GTR = -"&gt;".
	       GEQ = -"&gt;=".

{ Separators }
             -SEMI = S?, -";", S?.
            -COLON = S?, -":", S?.
           -EQUALS = S?, -"=", S?.</code></pre></div><h1>Test case: g41.c06</h1><details><p>Repository URI: <code>â€¦/tests/misc/misc-041-060-catalog.xml</code></p></details>
      <p>Created 15&nbsp;Jun&nbsp;2022 by cmsmcq</p>
      <div class="description">
	<p>Error case generated by random modification of g41.c00.</p>
	<p>In the Divide routine, the procedure call Write(y) was
	modified by inserting an instance of 'RecordType' before the
	expresssion 'y'.</p>
      </div>
      <div class="test-string"><h4>Input string (924 characters)</h4><pre><code>MODULE Samples;
(* Wirth 1995, pp. 37-38 *)

PROCEDURE Multiply;
  VAR x, y, z: INTEGER;
BEGIN Read(x); Read(y); z := 0;
  WHILE x &gt; 0 DO
    IF x MOD 2 = 1 THEN z := z + y END ;
    y := 2*y; x := x DIV 2
  END ;
  Write(x); Write(y); Write(z); WriteLn
END Multiply;

PROCEDURE Divide;
  VAR x, y, r, q, w: INTEGER;
BEGIN Read(x); Read(y); r := x; q := 0; w := y;
  WHILE w &lt;= r DO w := 2*w END ;
  WHILE w &gt; y DO
    q := 2*q; w := w DIV 2;
    IF w &lt;= r THEN r := r - w; q := q + 1 END
  END ;
  Write(x); Write(RECORD nogo, goon = a, b, c: real; y); Write(q); Write(r); WriteLn
END Divide;

PROCEDURE BinSearch;
  VAR i, j, k, n: INTEGER;
    a: ARRAY 32 OF INTEGER;
BEGIN Read(n); k := 0;
  WHILE k &lt; n DO Read(a[k]); k := k + 1 END ;
  i := 0; j := n;
  WHILE i &lt; j DO
    k := (i+j) DIV 2;
    IF x &lt; a[k] THEN j := k ELSE i := k + 1 END
  END ;
  Write(i); Write(j); Write(a[j]); WriteLn
END BinSearch;

END Samples.</code></pre></div>
      <div class="result unsuccessful"><h4>Expected result</h4><p class="not-a-sentence">The input does not match the grammar.</p></div>
    </main></body></html>